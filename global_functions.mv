<MvCOMMENT>
Functions
	GlobalField_Read( output var )
	Load_Global_CFM_ID( id, output var )
	Load_Global_CFM_Code( code, output var)
	Global_Insert( global_cfm var )
	Global_Insert_Lowlevel( global_cfm var )
	Global_Update( global_cfm var )
	Global_Update_LowLevel( global_cfm var )
	Global_Delete( id )

Future Item/ MvDO Functions
	GlobalFieldList_Load_All( fields var )
	Load_Global_Fields( custom_fields, output var)
	Load_Global_Fields_Runtime()

JSON Functions
	JSON_Global_CFS_Load_Query( module var )
	JSON_Global_Insert( module var )
	JSON_Global_Update( module var )
	JSON_Global_Delete( module var )
	JSON_Global_Preload( module var )
</MvCOMMENT>

<MvFUNCTION NAME = "GlobalField_Read" PARAMETERS = "output var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.output:id"		VALUE = "{ TGCFM_GlobalFields.d.id }">
	<MvASSIGN NAME = "l.output:code"	VALUE = "{ TGCFM_GlobalFields.d.code }">
	<MvASSIGN NAME = "l.output:name"	VALUE = "{ TGCFM_GlobalFields.d.name }">
	<MvASSIGN NAME = "l.output:value"	VALUE = "{ TGCFM_GlobalFields.d.value }">
	<MvASSIGN NAME = "l.output:preload"	VALUE = "{ TGCFM_GlobalFields.d.preload }">
</MvFUNCTION>

<MvFUNCTION NAME = "Load_Global_CFM_ID" PARAMETERS = "id, output var" STANDARDOUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGCFM_GlobalFields"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGCFM_GlobalFields WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGCFM-ERROR-0002', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGCFM_GlobalFields.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGCFM_GlobalFields">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'TGCFM-ERROR-0003' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ GlobalField_Read( l.output ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGCFM_GlobalFields">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Load_Global_CFM_Code" PARAMETERS = "code, output var" STANDARDOUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGCFM_GlobalFields"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGCFM_GlobalFields WHERE ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( '?' ) }"
				FIELDS	= "l.code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGCFM-ERROR-0004', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGCFM_GlobalFields.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGCFM_GlobalFields">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'TGCFM-ERROR-0005' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ GlobalField_Read( l.output ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGCFM_GlobalFields">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Global_Insert" PARAMETERS = "global_cfm var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.global_cfm" MEMBER = "code" VALUE = "{ trim( l.global_cfm:code ) }">
	<MvASSIGN NAME = "l.global_cfm" MEMBER = "name" VALUE = "{ trim( l.global_cfm:name ) }">
	<MvASSIGN NAME = "l.global_cfm" MEMBER = "value" VALUE = "{ trim( l.global_cfm:value ) }">
	<MvASSIGN NAME = "l.global_cfm" MEMBER = "preload" VALUE = "{ trim( l.global_cfm:preload ) }">

	<MvIF EXPR = "{ ISNULL l.global_cfm:code }">
		<MvASSIGN NAME = "g.Error_Code" VALUE = "code">
		<MvASSIGN NAME = "g.Error_Message" VALUE = "Code is required.">
		<MvFUNCTIONRETURN VALUE = 0>
	<MvELSEIF EXPR = "{ Load_Global_CFM_Code( l.global_cfm:code, l.temp_cfm ) }">
		<MvASSIGN NAME = "g.Error_Code" VALUE = "code">
		<MvASSIGN NAME = "g.Error_Message" VALUE = "{ 'The code ' $ l.global_cfm:code $ ' already exist.' }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.global_cfm:name }">
		<MvASSIGN NAME = "g.Error_Code" VALUE = "name">
		<MvASSIGN NAME = "g.Error_Message" VALUE = "Name is required.">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ Global_Insert_Lowlevel( l.global_cfm ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Global_Insert_Lowlevel" PARAMETERS = "global_cfm var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.global_cfm" MEMBER = "id" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'TGCFM_Fields' ) }">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'TGCFM_GlobalFields
					      ( id, code, name, value, preload )
						  VALUES
						  ( ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.global_cfm:id, l.global_cfm:code, l.global_cfm:name, l.global_cfm:value, l.global_cfm:preload">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGCFM-ERROR-0001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Global_Update" PARAMETERS = "global_cfm var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT Load_Global_CFM_ID( l.global_cfm:id, l.current_cfm ) }">
		<MvASSIGN NAME = "g.Error_Code" VALUE = "id">
		<MvASSIGN NAME = "g.Error_Message" VALUE = "Could not load Global Custom field.">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.global_cfm:code }">
		<MvASSIGN NAME = "g.Error_Code" VALUE = "code">
		<MvASSIGN NAME = "g.Error_Message" VALUE = "Code is required.">
		<MvFUNCTIONRETURN VALUE = 0>
	<MvELSEIF EXPR = "{ ( l.global_cfm:code NE l.current_cfm:code ) AND Load_Global_CFM_Code( l.global_cfm:code, l.temp_cfm ) }">
		<MvASSIGN NAME = "g.Error_Code" VALUE = "code">
		<MvASSIGN NAME = "g.Error_Message" VALUE = "{ 'The code ' $ l.global_cfm:code $ ' already exist.' }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.global_cfm:name }">
		<MvASSIGN NAME = "g.Error_Code" VALUE = "name">
		<MvASSIGN NAME = "g.Error_Message" VALUE = "Name is required.">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ Global_Update_Lowlevel( l.global_cfm ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Global_Update_Lowlevel" PARAMETERS = "global_cfm var" STANDARDOUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'TGCFM_GlobalFields
					      SET
							code = ?,
							name = ?,
							value = ?,
							preload = ?
					      WHERE
						    id			= ?' }"
			 FIELDS	= "l.global_cfm:code, l.global_cfm:name, l.global_cfm:value, l.global_cfm:preload, l.global_cfm:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGCFM-ERROR-0006', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Global_Delete" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'TGCFM_GlobalFields WHERE id = ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGCFM-ERROR-0007', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>



<MvFUNCTION NAME = "GlobalFieldList_Load_All" PARAMETERS = "fields var" STANDARDOUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGCFM_GlobalFields"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGCFM_GlobalFields ORDER BY id' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGCFM-ERROR-0008', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.field_count"		VALUE = 0>
	<MvWHILE EXPR = "{ NOT TGCFM_GlobalFields.d.EOF }">
		<MvASSIGN NAME = "l.field_count"	VALUE = "{ l.field_count + 1 }">

		<MvEVAL EXPR = "{ GlobalField_Read( l.fields[ l.field_count ] ) }">
		<MvSKIP NAME = "Merchant" VIEW = "TGCFM_GlobalFields" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGCFM_GlobalFields">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'TGCFM-ERROR-0009', l.field_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME ="Load_Global_Fields" PARAMETERS = "custom_fields, output var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ISNULL l.custom_fields }">
		<MvFOREACH ITERATOR = "l.field" ARRAY = "l.fields" INDEX = "l.pos" COUNT = "{ GlobalFieldList_Load_All( l.fields ) }">			
			<MvASSIGN NAME = "l.output" INDEX = "{ l.pos }" MEMBER = "code"		VALUE = "{ l.field:code }">
			<MvASSIGN NAME = "l.output" INDEX = "{ l.pos }" MEMBER = "name"		VALUE = "{ l.field:name }">
			<MvASSIGN NAME = "l.output" INDEX = "{ l.pos }" MEMBER = "value"	VALUE = "{ l.field:value }">
			<MvASSIGN NAME = "l.output" INDEX = "{ l.pos }" MEMBER = "preload"		VALUE = "{ l.field:preload }">
		</MvFOREACH>
	<MvELSE>
		<MvASSIGN NAME = "l.total_custom_fields" VALUE = "{ miva_splitstring( l.custom_fields, ',', l.custom_field_codes, l.flags ) }">

		<MvIF EXPR = "{ l.total_custom_fields EQ 1 }">
			<MvIF EXPR = "{ NOT Load_Global_CFM_Code( l.custom_fields, l.current_field ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
			<MvASSIGN NAME = "l.output" VALUE = "{ l.current_field:value }">
			<MvFUNCTIONRETURN VALUE = 1>
		<MvELSE>
			<MvFOREACH ITERATOR = "l.custom_field_code" ARRAY = "l.custom_field_codes">
				<MvIF EXPR = "{ Load_Global_CFM_Code( l.custom_field_code, l.current_field ) }">
					<MvASSIGN NAME = "l.output" MEMBER = "{ l.custom_field_code }" VALUE = "{ l.current_field:value }">
				</MvIF>
			</MvFOREACH>
		</MvIF>
	</MvIF>
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Load_Global_Fields_Runtime" STANDARDOUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGCFM_GlobalFields"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGCFM_GlobalFields WHERE preload = 1' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGCFM-ERROR-0010', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvWHILE EXPR = "{ NOT TGCFM_GlobalFields.d.EOF }">
		<MvASSIGN NAME = "l.void" VALUE = "{ GlobalField_Read( l.field ) }">
		<MvASSIGN NAME = "l.fields:customfield_values:customfields" MEMBER = "{ l.field:code }" VALUE = "{ l.field:value }">
		<MvASSIGN NAME = "l.fields:customfield_names:customfields" MEMBER = "{ l.field:code }" VALUE = "{ l.field:name }">
		<MvSKIP NAME = "Merchant" VIEW = "TGCFM_GlobalFields" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGCFM_GlobalFields">
	<MvFUNCTIONRETURN VALUE = "{ l.fields }">
</MvFUNCTION>


<MvFUNCTION NAME = "JSON_Global_CFS_Load_Query" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvASSIGN NAME = "g.Filter"			VALUE = "{ trim( g.Filter ) }">
	<MvASSIGN NAME = "g.Sort"			VALUE = "{ trim( g.Sort ) }">
	<MvASSIGN NAME = "g.Offset"			VALUE = "{ trim( g.Offset ) }">
	<MvASSIGN NAME = "g.Count"			VALUE = "{ trim( g.Count ) }">
	<MvASSIGN NAME = "l.search_query"	VALUE = "">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query,'s.*' ) }">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.search_query, g.Store_Table_Prefix $ 'TGCFM_GlobalFields', 's' ) }">

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Filter( l.search_query, g.Filter,'id:s.id,code:s.code,name:s.name,value:s.value' ) }">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_OrderBy_Fields(l.search_query, g.Sort, 'id:s.id,code:s.code,name:s.name,value:s.value', 's.id' ) }">

	<MvASSIGN NAME = "l.search_sql" VALUE = "{ [ g.Module_Library_DB].SQL_Query_Build( l.search_query, l.search_fields ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].SQL_Query_Count( l.search_query, l.total_count ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error(g.Error_Code, g.Error_Message ) }">
	<MvELSEIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range('Merchant', 'TGCFM_GlobalFields', l.search_sql, l.search_fields, g.Offset, g.Count) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'TGCFM-JSON-0001', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = 0>

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Response_Start() }">
		{
			"data":
				[
					<MvWHILE EXPR = "{ ( NOT TGCFM_GlobalFields.d.EOF ) AND ( ( g.Count EQ 0 ) OR (l.count LT g.Count ) ) }">
					<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_Start( l.count )}">
					"id": <MvEVAL EXPR = "{ int( TGCFM_GlobalFields.d.id ) }">,
					"code" : "<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( TGCFM_GlobalFields.d.code ) }">",
					"name" : "<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( TGCFM_GlobalFields.d.name ) }">",
					"value" : "<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( TGCFM_GlobalFields.d.value ) }">",
					"preload" : <MvEVAL EXPR = "{ int( TGCFM_GlobalFields.d.preload ) }">
					<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">
					<MvSKIP NAME = "Merchant" VIEW = "TGCFM_GlobalFields" ROWS = 1>
					</MvWHILE>
				],

			"total_count": <MvEVAL EXPR = "{ int( l.total_count ) }">,
			"start_offset": <MvEVAL EXPR = "{ int( g.Offset ) }">
		}
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGCFM_GlobalFields">
	}

</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Global_Insert" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.global_cfm" MEMBER = "code" VALUE = "{ g.Code }">
	<MvASSIGN NAME = "l.global_cfm" MEMBER = "name" VALUE = "{ g.Name }">
	<MvASSIGN NAME = "l.global_cfm" MEMBER = "value" VALUE = "{ g.Value }">
	<MvASSIGN NAME = "l.global_cfm" MEMBER = "preload" VALUE = "{ g.Preload }">

	<MvIF EXPR = "{ NOT Global_Insert( l.global_cfm ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Global_Update" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.global_cfm" MEMBER = "id" VALUE = "{ g.ID }">
	<MvASSIGN NAME = "l.global_cfm" MEMBER = "code" VALUE = "{ g.Code }">
	<MvASSIGN NAME = "l.global_cfm" MEMBER = "name" VALUE = "{ g.Name }">
	<MvASSIGN NAME = "l.global_cfm" MEMBER = "value" VALUE = "{ g.Value }">
	<MvASSIGN NAME = "l.global_cfm" MEMBER = "preload" VALUE = "{ g.Preload }">

	<MvIF EXPR = "{ NOT Global_Update( l.global_cfm ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Global_Delete" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT Global_Delete( g.ID ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'code', g.Error_Message ) }">
	</MvIF>
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Global_Preload" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT Load_Global_CFM_ID( g.CF_ID, l.global_cfm ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'code', g.Error_Message ) }">	
	</MvIF>
	<MvASSIGN NAME = "l.global_cfm" MEMBER = "preload" VALUE = "{ g.Preload }">
		
	<MvIF EXPR = "{ NOT Global_Update( l.global_cfm ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>
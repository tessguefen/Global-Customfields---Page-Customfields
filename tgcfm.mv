<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "compresswhitespace">
	<MvASSIGN NAME = "l.module:code"		VALUE = "TGCFM">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Global & Page Customfields">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Tess Guefen">
	<MvASSIGN NAME = "l.module:version"		VALUE = "1.000">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "9.00">
	<MvASSIGN NAME = "l.module:description"	VALUE = "Customfields in all the things and places and stuff.">
	<MvASSIGN NAME = "l.module:features"	VALUE = "data_store, vis_util, util, json, clientside, component, system">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Install_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGCFM_Fields', 1 ) }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>
	
	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGCFM_Groups', 1 ) }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>
	
	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGCFM_FieldOptions', 1 ) }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>
	

	<MvQUERY NAME	= "Merchant"
				 QUERY	= "{ 'CREATE TABLE ' $  g.Store_Table_Prefix $ 'TGCFM_GlobalFields
							  (
								id		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 ) 	$ ',
								code	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 ) 		$ ',
								name	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 100 ) 		$ ',
								value	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO()			$ ',
								preload	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ '
							  ) ' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGCFM-INSTALL-1000', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY NAME	= "Merchant"
				 QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGCFM_GlobalFields_1 ON ' $ g.Store_Table_Prefix $ 'TGCFM_GlobalFields ( id )' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGCFM-INSTALL-1001', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY NAME	= "Merchant"
				 QUERY	= "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'TGCFM_GlobalFields_2 ON ' $ g.Store_Table_Prefix $ 'TGCFM_GlobalFields ( code )' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGCFM-INSTALL-1002', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE TABLE ' $  g.Store_Table_Prefix $ 'TGCFM_PageFields
						  (
							id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 ) 	$ ',
							group_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
							code		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 ) 		$ ',
							name		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 100 ) 		$ ',
							fieldtype 	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 20 ) 		$ ',
							info		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO()			$ '
						  ) ' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGCFM-INSTALL-1005', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY NAME	= "Merchant"
				 QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGCFM_PageFields_1 ON ' $ g.Store_Table_Prefix $ 'TGCFM_PageFields ( id )' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGCFM-INSTALL-1006', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY NAME	= "Merchant"
				 QUERY	= "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'TGCFM_PageFields_2 ON ' $ g.Store_Table_Prefix $ 'TGCFM_PageFields ( code )' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGCFM-INSTALL-1007', g.MvQUERY_Error ) }">
		</MvIF>
		
		<MvQUERY NAME	= "Merchant"
				 QUERY	= "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'TGCFM_PageFields_3 ON ' $ g.Store_Table_Prefix $ 'TGCFM_PageFields ( group_id )' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGCFM-INSTALL-1008', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY NAME	= "Merchant"
				 QUERY	= "{ 'CREATE TABLE ' $  g.Store_Table_Prefix $ 'TGCFM_PageValues
							  (
								field_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								page_id		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								value		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO()			$ '
							  ) ' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGCFM-INSTALL-1009', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY NAME	= "Merchant"
				 QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGCFM_PageValues_1 ON ' $ g.Store_Table_Prefix $ 'TGCFM_PageValues ( page_id, field_id )' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGCFM-INSTALL-1010', g.MvQUERY_Error ) }">
		</MvIF>

		<MvCOMMENT>
		|
		| Groups
		|
		</MvCOMMENT>

		<MvQUERY NAME	= "Merchant"
				 QUERY	= "{ 'CREATE TABLE ' $  g.Store_Table_Prefix $ 'TGCFM_Groups
							  (
								id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								code 		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ ',
								name		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 100 ) 		$ '
							  )' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGCFM-INSTALL-1011', g.MvQUERY_Error ) }">
		</MvIF>
		
		<MvQUERY NAME	= "Merchant"
				 QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGCFM_Groups_1 ON ' $ g.Store_Table_Prefix $ 'TGCFM_Groups ( id )' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGCFM-INSTALL-1012', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY NAME	= "Merchant"
				 QUERY	= "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'TGCFM_Groups_2 ON ' $ g.Store_Table_Prefix $ 'TGCFM_Groups ( code )' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGCFM-INSTALL-1013', g.MvQUERY_Error ) }">
		</MvIF>

		<MvCOMMENT>
		|
		| FieldOptions
		|
		</MvCOMMENT>

		<MvQUERY NAME	= "Merchant"
				 QUERY	= "{ 'CREATE TABLE ' $  g.Store_Table_Prefix $ 'TGCFM_FieldOptions
							  (
								id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								field_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 ) 	$ ',
								value		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 ) 		$ '
							  )' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGCFM-INSTALL-1014', g.MvQUERY_Error ) }">
		</MvIF>
		
		<MvQUERY NAME	= "Merchant"
				 QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGCFM_FieldOptions_1 ON ' $ g.Store_Table_Prefix $ 'TGCFM_FieldOptions ( id )' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGCFM-INSTALL-1015', g.MvQUERY_Error ) }">
		</MvIF>
		
		<MvQUERY NAME	= "Merchant"
				 QUERY	= "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'TGCFM_FieldOptions_2 ON ' $ g.Store_Table_Prefix $ 'TGCFM_FieldOptions ( field_id )' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGCFM-INSTALL-1016', g.MvQUERY_Error ) }">
		</MvIF>
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Upgrade_Store" PARAMETERS = "module var, version" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Uninstall_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.void" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGCFM_Fields' ) }">
	<MvASSIGN NAME = "l.void" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGCFM_Groups' ) }">
	<MvASSIGN NAME = "l.void" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGCFM_FieldOptions' ) }">

	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $  g.Store_Table_Prefix $ 'TGCFM_GlobalFields' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $  g.Store_Table_Prefix $ 'TGCFM_GlobalValues' }">

	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $  g.Store_Table_Prefix $ 'TGCFM_PageFields' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $  g.Store_Table_Prefix $ 'TGCFM_PageValues' }">

	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $  g.Store_Table_Prefix $ 'TGCFM_Groups' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $  g.Store_Table_Prefix $ 'TGCFM_FieldOptions' }">

	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>


<MvCOMMENT>
| ========================================================================================================================
| Store Utilities Feature (util)
| ========================================================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "StoreUtilityModule_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_Action" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_Screen" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_LeftNavigation"  PARAMETERS = "module var, indent" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_Action_Privileges" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = -1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_Screen_Privileges" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = -1>
</MvFUNCTION>


<MvCOMMENT>
| ========================================================================================================================
|	Utility Screen Feature (vis_util)
| ========================================================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Utility_Content" PARAMETERS = "module var, tab, load_fields" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ l.tab EQ 'TGCFM_Global' }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_HTML() }">
		<div id="jsTGCFM_GlobalBatchlist"></div>
		<span id="jsTGCFM_GlobalBatchlist_mmbatchlist_persistent_filters" class="mm9_batchlist_action_row_section">
			<span style="font-size:1rem;">Global Customfields</span>
		</span>
		<MvFUNCTIONRETURN VALUE = 1 />
	</MvIF>
	<MvIF EXPR = "{ l.tab EQ 'TGCFM_Groups' }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_HTML() }">
		<div id="jsTGCFM_GroupsBatchlist"></div>
		<span id="jsTGCFM_GroupsBatchlist_mmbatchlist_persistent_filters" class="mm9_batchlist_action_row_section">
			<span style="font-size:1rem;">Page Customfield Groups</span>
		</span>
		<MvFUNCTIONRETURN VALUE = 1 />
	</MvIF>
	<MvIF EXPR = "{ l.tab EQ 'TGCFM_Pages' }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_HTML() }">
		<div id="jsTGCFM_PageFieldsBatchlist"></div>
		<span id="jsTGCFM_PageFieldsBatchlist_mmbatchlist_persistent_filters" class="mm9_batchlist_action_row_section">
			<span style="font-size:1rem;">Page Customfields</span>
		</span>
		<MvEVAL EXPR = "{ PageField_AddEditDialog_HTML( l.module ) }">
		<MvFUNCTIONRETURN VALUE = 1 />
	</MvIF>
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Utility_Head" PARAMETERS = "module var, tab" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ l.tab EQ 'TGCFM_Global' }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_JavaScript() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_CSS() }">
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Filename=tgcfm_global.js' }"></script>
		<script language="JavaScript">
			MMScreen_LoadFinished( function() { new TGCFM_GlobalBatchlist(); } );
		</script>
		<MvFUNCTIONRETURN VALUE = 1 />
	</MvIF>
	<MvIF EXPR = "{ l.tab EQ 'TGCFM_Groups' }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_JavaScript() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_CSS() }">
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Filename=tgcfm_page_groups.js' }"></script>
		<script language="JavaScript">
			MMScreen_LoadFinished( function() { new TGCFM_GroupsBatchlist(); } );
		</script>
		<MvFUNCTIONRETURN VALUE = 1 />
	</MvIF>
	<MvIF EXPR = "{ l.tab EQ 'TGCFM_Pages' }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_JavaScript() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_CSS() }">

		<MvEVAL EXPR = "{ PageField_AddEditDialog_CSS( l.module ) }">

		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Filename=tgcfm_page_popup.js' }"></script>
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Filename=tgcfm_page.js' }"></script>
		<script language="JavaScript">
			MMScreen_LoadFinished( function() { new PageFieldList(); } );
		</script>
		<MvFUNCTIONRETURN VALUE = 1 />
	</MvIF>
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Utility_Tabs" PARAMETERS = "module_var" STANDARDOUTPUTLEVEL = "compresswhitespace">
	<MvFUNCTIONRETURN VALUE = "TGCFM_Global: Global Customfields,TGCFM_Pages: Page Customfields,TGCFM_Groups: Page Customfield Groups" />
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Utility_Update" PARAMETERS = "module_var" STANDARDOUTPUTLEVEL = "compresswhitespace">
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Utility_Validate" PARAMETERS = "module_var" STANDARDOUTPUTLEVEL = "compresswhitespace">
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvCOMMENT>
| ========================================================================================================================
|	CLIENTSIDE
| ========================================================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Clientside" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, compresswhitespace">
	<MvIF EXPR = "{ g.Filename EQ 'tgcfm_global.js' }">
		<MvEVAL EXPR = "{ [ g.Module_Clientside ].Module_Content_Type( l.module, 'text/javascript' ) }">
		<MvINCLUDE FILE = "js/tgcfm_global.js">
	</MvIF>
	<MvIF EXPR = "{ g.Filename EQ 'tgcfm_page_groups.js' }">
		<MvEVAL EXPR = "{ [ g.Module_Clientside ].Module_Content_Type( l.module, 'text/javascript' ) }">
		<MvINCLUDE FILE = "js/tgcfm_page_groups.js">
	</MvIF>
	<MvIF EXPR = "{ g.Filename EQ 'tgcfm_page.js' }">
		<MvEVAL EXPR = "{ [ g.Module_Clientside ].Module_Content_Type( l.module, 'text/javascript' ) }">
		<MvINCLUDE FILE = "js/tgcfm_page.js">
	</MvIF>
	<MvIF EXPR = "{ g.Filename EQ 'tgcfm_page_popup.js' }">
		<MvEVAL EXPR = "{ [ g.Module_Clientside ].Module_Content_Type( l.module, 'text/javascript' ) }">
		<MvINCLUDE FILE = "js/tgcfm_page_popup.js">
	</MvIF>
</MvFUNCTION>


<MvCOMMENT>
| ========================================================================================================================
|	JSON
| ========================================================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "Module_JSON" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT g.Admin_Open_Store }"><MvFUNCTIONRETURN></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'JSON_Global_CFS_Load_Query'}"><MvFUNCTIONRETURN VALUE = "{ JSON_Global_CFS_Load_Query( l.module )	}"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'TGCFS_Global_Insert' }"><MvFUNCTIONRETURN VALUE = "{ JSON_Global_Insert( l.module ) }"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'TGCFS_Global_Update' }"><MvFUNCTIONRETURN VALUE = "{ JSON_Global_Update( l.module ) }"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'TGCFS_Global_Delete' }"><MvFUNCTIONRETURN VALUE = "{ JSON_Global_Delete( l.module ) }"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'TGCFS_Global_Preload' }"><MvFUNCTIONRETURN VALUE = "{ JSON_Global_Preload( l.module ) }"></MvIF>

	<MvIF EXPR = "{ g.Module_Function EQ 'JSON_Groups_Load_Query' }"><MvFUNCTIONRETURN VALUE = "{ JSON_Groups_Load_Query( l.module ) }"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'TGCFS_Group_Insert' }"><MvFUNCTIONRETURN VALUE = "{ JSON_Group_Insert( l.module )	}"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'TGCFS_Group_Update' }"><MvFUNCTIONRETURN VALUE = "{ JSON_Group_Update( l.module )	}"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'TGCFS_Group_Delete' }"><MvFUNCTIONRETURN VALUE = "{ JSON_Group_Delete( l.module )	}"></MvIF>

	<MvIF EXPR = "{ g.Module_Function EQ 'JSON_PageFields_Load_Query' }"><MvFUNCTIONRETURN VALUE = "{ JSON_PageFields_Load_Query( l.module ) }"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'PageField_Insert' }"><MvFUNCTIONRETURN VALUE = "{ JSON_PageField_Insert( l.module ) }"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'PageField_Update' }"><MvFUNCTIONRETURN VALUE = "{ JSON_PageField_Update( l.module ) }"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'PageField_Delete' }"><MvFUNCTIONRETURN VALUE = "{ JSON_PageField_Delete( l.module ) }"></MvIF>

	<MvIF EXPR = "{ g.Module_Function EQ 'JSON_GroupList_Load_All' }"><MvFUNCTIONRETURN VALUE = "{ JSON_GroupList_Load_All( l.module ) }"></MvIF>
</MvFUNCTION>


<MvCOMMENT>
| ====================================================================================
|	COMPONENT
| ====================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.void" VALUE = "{ PageField_Initialize( l.all_settings:page:id, l.all_settings ) }">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT GroupList_Load_Order( l.groups ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
		
		<MvFUNCTIONRETURN VALUE = "">
	</MvIF>

	<MvASSIGN NAME = "l.tabs" VALUE = "">

	<MvFOREACH ITERATOR = "l.group" ARRAY = "l.groups" INDEX = "l.pos">
		<MvIF EXPR = "{ l.pos GT 1 }">
			<MvASSIGN NAME = "l.tabs" VALUE = "{ l.tabs $ ',' }">
		</MvIF>
		
		<MvIF EXPR = "{ l.group:id EQ 0 }">
			<MvASSIGN NAME = "l.tabs" VALUE = "{ l.tabs $ 'TGCFM_default:Custom Fields' }">
		<MvELSE>
			<MvASSIGN NAME = "l.tabs" VALUE = "{ l.tabs $ 'TGCFM_' $ l.group:code $ ':' $ l.group:name }">
		</MvIF>
	</MvFOREACH>
	
	<MvFUNCTIONRETURN VALUE = "{ l.tabs }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "html, text">
	<MvIF EXPR = "{ ISNULL g.TGCFM_Fields }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Page_Load_Code( g.Edit_Page, l.current_page ) }">
		<MvFUNCTIONRETURN VALUE = 1 />
	</MvIF>

	<MvFOREACH ITERATOR = "l.field" ARRAY = "g.TGCFM_Fields">
		<MvEVAL EXPR = "{ Normalize_Value( l.field, l.field:value ) }">

		<MvIF EXPR = "{ len_var( l.field:value ) }">
			<MvIF EXPR = "{ NOT PageFieldValue_Set( l.field:id, l.current_page:id, l.field:value ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
			
			<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'TGCFM-1000', 'Page CustomFields \'' $ l.current_page:code $ '\', Custom Field \'' $ l.field:code $ '\' updated' ) }">
		</MvIF>
	</MvFOREACH>
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "text,html,compresswhitespace">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Page_Load_Code( g.Edit_Page, l.current_page ) }">
		<MvFUNCTIONRETURN VALUE = 1 />
	</MvIF>
	<MvASSIGN NAME = "l.group_count"	VALUE = "{ GroupList_Load_Include_Default( l.groups ) }">
	<MvASSIGN NAME = "l.can_upload" VALUE = "{ [ g.Module_Admin ].CanI( 'PAGE', 0, 0, 1, 0 ) }">
	
	<MvFOREACH ITERATOR = "l.group" ARRAY = "l.groups" INDEX = "l.pos" COUNT = "{ l.group_count }">
		<MvIF EXPR = "{ l.tab EQ 'TGCFM_' $ l.group:code }">
			<MvEVAL EXPR = "{ PageCustom_Field_Content( l.current_page:id, l.group:id, l.can_upload, 0 ) }">
		</MvIF>
	</MvFOREACH>
	
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Prerender" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MVFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, settings var, item_settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT Parse_Function_Parameters( l.param, l.function_name, l.parameters, l.parameter_count ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMENT>
	|
	|	Parameters & Function check
	|
	</MvCOMMENT>


	<MvIF EXPR = "{ ( l.function_name EQ 'load_global_fields' )			OR
					( l.function_name EQ 'load_pagecode_fields' )		OR
					( l.function_name EQ 'load_pageid_fields' )
				}">
		<MvASSIGN NAME = "l.new_param" VALUE = "{ 'Component_' $ l.function_name $ '( l.module, l.param, l.settings, ' $ l.parameters $ ' )' }">
	</MvIF>

	<MvCOMMENT>
	|
	|	tagerror
	|	22, 3 = display
	|	22, 0 = no display
	|
	|	FOR DEBUGGING, leave as 22, 3
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ ISNULL l.new_param }">
		<MvFUNCTIONRETURN VALUE = "" />
	</MvIF>

	<MvASSEMBLY>
		.string asm_0 "l.new_param"
		.string asm_1 "g.Module_Root"
		.string asm_2 "l.module"
		.string asm_3 "module"
		.string asm_4 "g.MvDO_Error"

			pushc		asm_1
			pushn
			pushc		asm_2
			pushn
			pushc		asm_3
			memb_ro
			cat
			pushc		asm_0
			pushn
			tagerror	22, 0
			do_function
			pop
			tagerror	22, 0
			pushc		asm_4
			pushn
			jmp_eq		L_asm_success
			retn
		L_asm_success:
	</MvASSEMBLY>
</MvFUNCTION>

<MvFUNCTION NAME = "SystemModule_Screen" PARAMETERS = "module var, screen" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.void" VALUE = "{ Load_Global_Fields_Runtime( g.Store ) }">
	<MvFUNCTIONRETURN VALUE = "1">
</MvFUNCTION>

<MvFUNCTION NAME = "SystemModule_Action" PARAMETERS = "module var, action" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "1">
</MvFUNCTION>

<MvFUNCTION NAME = "SystemModule_UIException" PARAMETERS = "module var, exception" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "1">
</MvFUNCTION>


<MvINCLUDE FILE = "global_functions.mv">
<MvINCLUDE FILE = "group_functions.mv">
<MvINCLUDE FILE = "page_functions.mv">
<MvINCLUDE FILE = "values_functions.mv">
<MvINCLUDE FILE = "helpers.mv">
